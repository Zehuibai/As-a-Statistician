# Proc Sqplot

## Scatter Plot

### Vector in scatter Plot

```
*** calculate mean vector;
data VENUS_F8_2_3_6_D04;
    length label_SIA label_TIA label_DV $100.;
    set VENUS_F8_2_3_6_D03;
	by  RD_IOL_TYP visit; 
    if first.visit then do;
	    *** SIA;
		tot_X13   = 0;
		tot_Y13   = 0;
		SIA_Index = 0;
		*** TIA;
		tot_X12   = 0;
		tot_Y12   = 0;
		TIA_Index = 0;
		*** DV;
		tot_X32   = 0;
		tot_Y32   = 0;
		DV_Index = 0;
	end;

	*** SIA;
	tot_X13 + X13;
	tot_Y13 + Y13;
	SIA_Index + 1;
	*** TIA;
	tot_X12 + X12;
	tot_Y12 + Y12;
	TIA_Index + 1;
	*** DV;
	tot_X32 + X32;
	tot_Y32 + Y32;
	DV_Index + 1;

	if last.visit then do;
	  mean_X13 = tot_X13 / SIA_Index;
	  mean_Y13 = tot_Y13 / SIA_Index;
	  mean_X12 = tot_X12 / TIA_Index;
	  mean_Y12 = tot_Y12 / TIA_Index;
	  mean_X32 = tot_X32 / DV_Index;
	  mean_Y32 = tot_Y32 / DV_Index;
	end;

	if mean_X13 ne . then label_SIA = "mean(SIA) = "||"("||strip(put(round(mean_X13,0.01),5.2))||", "||strip(put(round(mean_Y13,0.01),5.2))||")";
	if mean_X12 ne . then label_TIA = "mean(TIA) = "||"("||strip(put(round(mean_X12,0.01),5.2))||", "||strip(put(round(mean_Y12,0.01),5.2))||")";
	if mean_X32 ne . then label_DV  = "mean(DV) = "||"("||strip(put(round(mean_X32,0.01),5.2))||", "||strip(put(round(mean_Y32,0.01),5.2))||")";

	drop tot_X13 tot_Y13 SIA_Index tot_X12 tot_Y12 TIA_Index tot_X32 tot_Y32 DV_Index;
run;



*** Head number;
*** Visit 5 1 month;
data _NULL_; set VENUS_F8_2_3_6_D03 (where=(RD_IOL_TYP eq 1 and Visit eq 5)) end=eof; if eof then call symputx('num_ITT_LARA_5',_N_); run;
data _NULL_; set VENUS_F8_2_3_6_D03 (where=(RD_IOL_TYP eq 2 and Visit eq 5)) end=eof; if eof then call symputx('num_ITT_TECN_5',_N_); run;
*** Visit 6 4-6 months;
data _NULL_; set VENUS_F8_2_3_6_D03 (where=(RD_IOL_TYP eq 1 and Visit eq 6)) end=eof; if eof then call symputx('num_ITT_LARA_6',_N_); run;
data _NULL_; set VENUS_F8_2_3_6_D03 (where=(RD_IOL_TYP eq 2 and Visit eq 6)) end=eof; if eof then call symputx('num_ITT_TECN_6',_N_); run;
*** Check macro variable;
%put &num_ITT_LARA_5;
%put &num_ITT_TECN_5;
%put &num_ITT_LARA_6;
%put &num_ITT_TECN_6;


*** graphic formats;
proc format;
	value category_a     1 = "AT LARA toric 929MP (N = &num_ITT_LARA_5)"
					     2 = "TECNIS Symfony toric (N = &num_ITT_TECN_5)";
	value category_b     1 = "AT LARA toric 929MP (N = &num_ITT_LARA_6)"
				         2 = "TECNIS Symfony toric (N = &num_ITT_TECN_6)";
run;

*** Report;



ods proclabel="Figure 8.2.3.6.1: Vector analysis (SIA)";
options orientation = landscape;

ods tagsets.rtf file="Z:\STUDIES\ZEISS\VENUS\WORKSPACE\07_STATISTIC\04_Statistical_Analysis\01_Final\02_Output\02_Figures\VENUS_F8_2_3_6_1_VectorSIA_fin_D01_0_0.rtf"
	style=gcpservice_style;
     
ods escapechar="^";	
ods exclude none;
option nobyline;

*** Graphical output;
ods graphics on;
ods graphics / height=13cm width=25cm border=off;
 
 
*** SIA: X13 Y13;
*** M1;
ods  text = '^S={width=100%}Figure 8.2.3.6.1:^R/RTF"\tab" Performance and safety data
                                           ^n^R/RTF"\tab\tab\tab" Other secondary endpoint
                                           ^n^R/RTF"\tab\tab\tab" Vector analysis (SIA)
										   ^n^nAnalysis set: SES, Level: Eye, Visit: Postoperative 1 month';
proc sgplot data=Venus_f8_2_3_6_d04_m1 /*sganno=class_annotations NOAUTOLEGEND*/;
    styleattrs datasymbols=(circlefilled squarefilled); 
	scatter x=X13  y=Y13 /group=RD_IOL_TYP groupdisplay=cluster markerattrs=(size=5px);
	vector y=mean_Y13 x=mean_X13 / group=RD_IOL_TYP nomissinggroup datalabel=label_sia arrowheadshape=filled;
    xaxis label = 'X (SIA)';
	yaxis label = 'Y (SIA)';
    keylegend / location=outside position=bottom across=2;
	format RD_IOL_TYP category_a.;
run;
ods text = "^R/RTF'\ql' Output generated by program &this_prog.";
ods startpage= now; 
footnote; 
title;


ods graphics off;
ods exclude all;

ods tagsets.rtf close;
ods _all_ close;
```

